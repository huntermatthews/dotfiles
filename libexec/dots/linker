#! /usr/bin/env fish

set -g HELP "\
Usage: $(status basename) [options...]

make symlinks for *.symlink files.

Optional Arguments
        --debug              Print debug information
    -h, --help               Print this help
"

set -g DOTS_LIBEXEC $DOTS/libexec/dots

source $DOTS_LIBEXEC/common-lib.fish
or begin
    echo "FATAL: could not source common-lib.fish -- exiting program..." 1>&2
    exit 11
end

argparse --name (status basename) debug h/help -- $argv
or begin
    echo $HELP
    exit 1
end

if set -q _flag_debug
    set -g FLAG_DEBUG true
end

if set -q _flag_help
    echo $HELP
    exit 0
end

echo "==> Building the symlinks..."

set symlinks (string split ' ' (dots_files_with_active_profile .symlink )) $DOTS_LOCAL/*.symlink
for src in $symlinks
    # Convert the src roughly into the dest
    set dst $HOME/.(string replace -r '\.symlink$' '' (basename $src))

    # Sadly, we can't just dump everything in $HOME anymore ... ~/.config is a thing and so is ~/.ssh
    set dst (string replace --all % / $dst)
    if test -L $dst
        echo "-> $dst already exists, skipping..."
        continue
    else if test -e $dst
        echo "-> WARNING: $dst exists..."
        continue
    else
        echo "-> $dst does not exist, creating..."
        ln -s $src $dst
    end

end

# Future

# Check for existing links in homedir + .local +  .config that point INTO the dots repo that aren't valid and
# remove them. This is a bit of a pain, but it would be nice to have a clean slate.

## END OF LINE ##
