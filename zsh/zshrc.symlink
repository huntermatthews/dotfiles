# -*- mode: sh -*-

# shortcut to this dotfiles path is $DOTFILES
export DOTFILES=$HOME/.dotfiles

# My one addition to this - trying to nail down the path to exactly what I want it to be
# and anything else I want all the other files to be able to depend on 
for file in $DOTFILES/zsh/*.startup
do 
    source $file
done

# all of our zsh files
typeset -U config_files
config_files=($DOTFILES/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files file

# Last second cleanup of path elements that don't exist (shouldn't happen but meh)
path=($^path(N))

## END OF LINE ##
