# -*- mode: sh -*-

# shortcut to this dotfiles path is $DOTFILES
export DOTFILES=$HOME/.dotfiles
export DOTFILES_DEBUG=False

dotfiles_debug()
{
    if [[ -n "$DOTFILES_DEBUG" && "$DOTFILES_DEBUG" == "True" ]] ; then
        echo "$@"
    fi
}

dotfiles_debug running .zshenv

# special code to cleanup homedirs that get synced with cp
typeset -U zshenv_cleanup_files
zshenv_cleanup_files=($DOTFILES/**/*.cleanup(N))
for file in ${zshenv_cleanup_files} ; do
    dotfiles_debug sourcing $file
    source $file
    ## removing the cleanup files was too much trouble to be worth it
done

# all of our zshenv files
typeset -U zshenv_files
zshenv_files=($DOTFILES/**/*.zshenv)

# and load them
for file in ${zshenv_files} ; do 
    dotfiles_debug sourcing $file
    source $file
done

unset zshenv_files zshenv_cleanup_files file

# Last second cleanup of path elements that don't exist (shouldn't happen but meh)
path=($^path(N))

## END OF LINE ##
